// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.23;

import "@openzeppelin/contracts/utils/Create2.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

import "./MyAccount.sol";

contract MyAccountFactory {
    MyAccount public immutable accountImplementation;

    constructor(IEntryPoint _entryPoint) {
        accountImplementation = new MyAccount(_entryPoint);
    }

    function createAccount(bytes32 _ownershipCommitment,uint256 salt) public returns (MyAccount ret) {
        address addr = getAddress(_ownershipCommitment, salt);
        uint256 codeSize = addr.code.length;
        if (codeSize > 0) {
            return MyAccount(payable(addr));
        }
        ret = MyAccount(payable(new ERC1967Proxy{salt : bytes32(salt)}(
                address(accountImplementation),
                abi.encodeCall(MyAccount.initialize, (_ownershipCommitment))
            )));
    }

    function getAddress(bytes32 _ownershipCommitment,uint256 salt) public view returns (address) {
        return Create2.computeAddress(
            bytes32(salt), 
            keccak256(
                abi.encodePacked(type(ERC1967Proxy).creationCode,
                abi.encode(
                    address(accountImplementation),
                    abi.encodeCall(
                        MyAccount.initialize, 
                        (_ownershipCommitment)
                    )
                )
            )));
    }
}